/-
Copyright (c) 2025 Alex Meiburg. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Alex Meiburg
-/
import QuantumInfo.ForMathlib.HermitianMat.Trace

namespace HermitianMat

open ComplexOrder
open scoped Matrix

variable {𝕜 : Type*} [RCLike 𝕜]
variable {n m ι : Type*} [Fintype n] [Fintype m] [Fintype ι]
variable {A B C : HermitianMat n 𝕜}
variable {M : Matrix m n 𝕜} {N : Matrix n n 𝕜}

open MatrixOrder in
/-- The `MatrixOrder` instance for Matrix (the Loewner order) we keep open for
HermitianMat, always. -/
instance : PartialOrder (HermitianMat n 𝕜) :=
  inferInstanceAs (PartialOrder (selfAdjoint _))

open MatrixOrder in
instance : IsOrderedAddMonoid (HermitianMat n 𝕜) :=
  inferInstanceAs (IsOrderedAddMonoid (selfAdjoint _))

theorem le_iff : A ≤ B ↔ (B - A).mat.PosSemidef := by
  rfl

theorem zero_le_iff : 0 ≤ A ↔ A.mat.PosSemidef := by
  rw [le_iff, sub_zero]

theorem le_iff_mulVec_le : A ≤ B ↔
    ∀ x, star x ⬝ᵥ A.mat *ᵥ x ≤ star x ⬝ᵥ B.mat *ᵥ x := by
  simp [le_iff, Matrix.PosSemidef, B.H.sub A.H, Matrix.sub_mulVec]

instance [DecidableEq n] : ZeroLEOneClass (HermitianMat n 𝕜) where
  zero_le_one := by
    rw [zero_le_iff]
    exact Matrix.PosSemidef.one

theorem lt_iff_posdef : A < B ↔ (B - A).mat.PosSemidef ∧ A ≠ B :=
  lt_iff_le_and_ne

instance : IsStrictOrderedModule ℝ (HermitianMat n 𝕜) where
  smul_lt_smul_of_pos_left a ha b b₂ hb := by
    rw [HermitianMat.lt_iff_posdef] at hb ⊢
    simp only [← smul_sub, ne_eq, smul_right_inj ha.ne']
    exact ⟨hb.left.smul ha.le, hb.right⟩
  smul_lt_smul_of_pos_right a ha b b2 hb := by
    rw [HermitianMat.lt_iff_posdef] at ha ⊢
    rw [sub_zero] at ha
    rw [← sub_pos] at hb
    convert And.intro (ha.left.smul hb.le) ha.right using 1
    · simp [← sub_smul]
    simp only [ne_eq, not_iff_not]
    constructor
    · intro h
      rw [eq_comm, ← sub_eq_zero, ← sub_smul] at h
      simpa [eq_comm, hb.ne'] using h
    · rintro rfl; simp

theorem posSemidef_iff_spectrum_Ici [DecidableEq n] (A : HermitianMat n 𝕜) :
    0 ≤ A ↔ spectrum ℝ A.mat ⊆ Set.Ici 0 := by
  rw [zero_le_iff, Matrix.posSemidef_iff_isHermitian_and_spectrum_nonneg]
  simp [A.H, Set.Ici.eq_1]

theorem posSemidef_iff_spectrum_nonneg [DecidableEq n] (A : HermitianMat n 𝕜) :
    0 ≤ A ↔ ∀ x ∈ spectrum ℝ A.mat, 0 ≤ x := by
  exact A.posSemidef_iff_spectrum_Ici

theorem trace_nonneg (hA : 0 ≤ A) : 0 ≤ A.trace := by
  exact (RCLike.nonneg_iff.mp (zero_le_iff.mp hA).trace_nonneg).1

theorem trace_pos (hA : 0 < A) : 0 < A.trace := by
  open ComplexOrder in
  have hA' := hA.le
  rw [HermitianMat.zero_le_iff] at hA'
  have h_pos := Matrix.PosSemidef.trace_pos hA' (by simpa [HermitianMat.ext_iff] using hA.ne')
  rw [HermitianMat.trace_eq_re_trace]
  rw [RCLike.pos_iff] at h_pos
  exact h_pos.left

open Lean Meta Mathlib.Meta.Positivity in
/-- Positivity extension for `HermitianMat.trace`: nonneg when the matrix is nonneg,
positive when the matrix is positive. -/
@[positivity HermitianMat.trace _]
def evalHermitianMatTrace : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app _tr (A : Expr) ← whnfR e | throwError "not HermitianMat.trace"
  let (isStrict, pfA) ← bestResult A
  if isStrict then
    pure (.positive (← mkAppM ``HermitianMat.trace_pos #[pfA]))
  else
    pure (.nonnegative (← mkAppM ``HermitianMat.trace_nonneg #[pfA]))

--Without these shortcut instances, `gcongr` fails to close certain goals...? Why? TODO
instance : PosSMulMono ℝ (HermitianMat n 𝕜) := inferInstance
instance : SMulPosMono ℝ (HermitianMat n 𝕜) := inferInstance

--Without explicitly giving this instance, Lean times out trying to find it sometimes.
instance : PosSMulReflectLE ℝ (HermitianMat n 𝕜) :=
  PosSMulMono.toPosSMulReflectLE

theorem le_trace_smul_one [DecidableEq n] (hA : 0 ≤ A) : A ≤ A.trace • 1 := by
  have hA' : A.mat.PosSemidef := zero_le_iff.mp hA
  refine (Matrix.PosSemidef.le_smul_one_of_eigenvalues_iff hA'.1 A.trace).mp ?_
  rw [← A.sum_eigenvalues_eq_trace]
  intro i
  exact Finset.single_le_sum (fun j _ ↦ hA'.eigenvalues_nonneg j) (Finset.mem_univ i)

/-- The Kronecker product of two nonnegative Hermitian matrices is nonnegative. -/
theorem kronecker_nonneg {A : HermitianMat m 𝕜} (hA : 0 ≤ A) (hB : 0 ≤ B) : 0 ≤ A ⊗ₖ B := by
  rw [zero_le_iff, kronecker_mat]
  classical exact (zero_le_iff.mp hA).PosSemidef_kronecker (zero_le_iff.mp hB)

/-- The Kronecker product of two positive Hermitian matrices is positive. -/
theorem kronecker_pos {A : HermitianMat m 𝕜} (hA : 0 < A) (hB : 0 < B) : 0 < A ⊗ₖ B := by
  apply lt_of_le_of_ne (kronecker_nonneg hA.le hB.le)
  intro h
  replace h := congr(trace $h)
  simp only [trace_zero, trace_kronecker, zero_eq_mul] at h
  apply trace_pos at hA
  apply trace_pos at hB
  grind only [cases Or]

open Lean Meta Mathlib.Meta.Positivity in
/-- Positivity extension for `HermitianMat.kronecker`: nonneg when both factors are. -/
@[positivity HermitianMat.kronecker _ _]
def evalHermitianMatKronecker : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app (.app _kron A) B ← whnfR e | throwError "not HermitianMat.kronecker"
  let (isStrictA, pfA) ← bestResult A
  let (isStrictB, pfB) ← bestResult B
  if isStrictA && isStrictB then
    pure (.positive (← mkAppM ``HermitianMat.kronecker_pos #[pfA, pfB]))
  else
    let pfA' ← try mkAppM ``le_of_lt #[pfA] catch _ => pure pfA
    let pfB' ← try mkAppM ``le_of_lt #[pfB] catch _ => pure pfB
    let pfAB' ← mkAppM ``HermitianMat.kronecker_nonneg #[pfA', pfB']
    pure (.nonnegative pfAB')

variable (M) in
open Lean Meta Mathlib.Meta.Positivity in
/-- Positivity extension for `HermitianMat.conj`: nonneg when the inner matrix is. -/
theorem conj_nonneg (hA : 0 ≤ A) : 0 ≤ A.conj M := by
  rw [zero_le_iff] at hA ⊢
  exact Matrix.PosSemidef.mul_mul_conjTranspose_same hA M

theorem conj_pos [DecidableEq n] {A : HermitianMat n 𝕜} {M : Matrix m n 𝕜} (hA : 0 < A)
    (h : LinearMap.ker M.toEuclideanLin ≤ A.ker) : 0 < A.conj M := by
  classical exact (A.conj_nonneg M hA.le).lt_of_ne' (A.conj_ne_zero hA.ne' h)

open Lean Meta Mathlib.Meta.Positivity in
/-- Positivity extension for `HermitianMat.conj`: nonneg when the inner matrix is. -/
@[positivity HermitianMat.conj _ _]
def evalHermitianMatConj : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app (.app _coe conjM) (A : Expr) ← whnfR e | throwError "not conj application"
  let M := conjM.appArg!
  let (_, pfA) ← bestResult A
  let pfNonneg ← try mkAppM ``le_of_lt #[pfA] catch _ => pure pfA
  pure (.nonnegative (← mkAppM ``HermitianMat.conj_nonneg #[M, pfNonneg]))

example [DecidableEq n] [DecidableEq m] [Nonempty n] [Nonempty m]
  (A B : HermitianMat n ℂ) (hA : 0 ≤ A) (hB : 0 ≤ B) (M : Matrix m n ℂ) :
    0 < (2 : HermitianMat (n × m) ℂ) + (3 • A) ⊗ₖ (Real.pi • B).conj M := by
  positivity

example (A B : HermitianMat n ℂ) (hA : 0 < A) (hB : 0 < B) :
    0 < ((37 • A) ⊗ₖ ((38 : ℝ) • B)).trace := by
  positivity

theorem convex_cone (hA : 0 ≤ A) (hB : 0 ≤ B) {c₁ c₂ : ℝ} (hc₁ : 0 ≤ c₁) (hc₂ : 0 ≤ c₂) :
    0 ≤ (c₁ • A + c₂ • B) := by
  rw [zero_le_iff] at hA hB ⊢
  exact (hA.smul hc₁).add (hB.smul hc₂)

theorem sq_nonneg [DecidableEq n] : 0 ≤ A ^ 2 := by
  simp [zero_le_iff, pow_two]
  nth_rewrite 1 [←Matrix.IsHermitian.eq A.H]
  exact Matrix.posSemidef_conjTranspose_mul_self A.mat

theorem ker_antitone [DecidableEq n] (hA : 0 ≤ A) : A ≤ B → B.ker ≤ A.ker := by
  intro h x hB
  replace h := (le_iff_mulVec_le.mp h) x
  rw [HermitianMat.mem_ker_iff_mulVec_zero] at hB ⊢
  rw [hB, dotProduct_zero] at h
  rw [zero_le_iff] at hA
  rw [← hA.dotProduct_mulVec_zero_iff]
  exact le_antisymm h (hA.right x)

theorem conj_mono (h : A ≤ B) : A.conj M ≤ B.conj M := by
  have h_conj_pos : (M * (B - A).mat * Mᴴ).PosSemidef :=
    Matrix.PosSemidef.mul_mul_conjTranspose_same h M
  constructor;
  · simp [conj, Matrix.IsHermitian, Matrix.mul_assoc]
  · simpa [Matrix.mul_sub, Matrix.sub_mul] using h_conj_pos.2

lemma conj_posDef [DecidableEq n] (hA : A.mat.PosDef) (hN : IsUnit N) :
    (A.conj N).mat.PosDef := by
  use HermitianMat.H _
  intro x hx_ne_zero
  open Matrix in
  have h_pos : 0 < star (Nᴴ *ᵥ x) ⬝ᵥ A *ᵥ (Nᴴ *ᵥ x) := by
    apply hA.2
    intro h
    apply hx_ne_zero
    simpa [ hN ] using Matrix.eq_zero_of_mulVec_eq_zero
      (by simpa [Matrix.det_conjTranspose] using hN.map Matrix.detMonoidHom) h
  convert h_pos using 1
  simp only [conj_apply_mat, mulVec_mulVec, Matrix.mul_assoc]
  simp [dotProduct_mulVec, mulVec_conjTranspose]

lemma inv_conj [DecidableEq n] {M : Matrix n n 𝕜} (hM : IsUnit M) :
    (A.conj M)⁻¹ = A⁻¹.conj (M⁻¹)ᴴ := by
  have h_inv : (M⁻¹)ᴴ * Mᴴ = 1 := by
    simp only [Matrix.isUnit_iff_isUnit_det, isUnit_iff_ne_zero, ne_eq] at hM
    simp [Matrix.conjTranspose_nonsing_inv, hM]
  ext1
  simp only [conj, AddMonoidHom.coe_mk, ZeroHom.coe_mk, Matrix.conjTranspose_conjTranspose]
  simp only [mat_inv, mat_mk]
  rw [Matrix.mul_inv_rev, Matrix.mul_inv_rev, Matrix.inv_eq_left_inv h_inv, mul_assoc]

theorem le_iff_mulVec_le_mulVec (A B : HermitianMat n 𝕜) :
    A ≤ B ↔ ∀ v : n → 𝕜, star v ⬝ᵥ A.mat *ᵥ v ≤ star v ⬝ᵥ B.mat *ᵥ v := by
  rw [← sub_nonneg, HermitianMat.zero_le_iff]
  conv_rhs => enter [v]; rw [← sub_nonneg]
  have h := (B - A).H
  simp only [HermitianMat.mat_sub] at h
  simp [Matrix.PosSemidef, Matrix.sub_mulVec, h]

theorem inner_mulVec_nonneg (hA : 0 ≤ A) (v : n → 𝕜) :
    0 ≤ star v ⬝ᵥ A.mat *ᵥ v := by
  rw [le_iff_mulVec_le_mulVec] at hA
  simpa using hA v

theorem mem_ker_of_inner_mulVec_zero [DecidableEq n] (hA : 0 ≤ A) (v : n → 𝕜)
    (h : star v ⬝ᵥ A.mat *ᵥ v = 0) : v ∈ A.ker := by
  exact ((zero_le_iff.mp hA).dotProduct_mulVec_zero_iff v).mp h

theorem ker_add [DecidableEq n] (hA : 0 ≤ A) (hB : 0 ≤ B) :
    (A + B).ker = A.ker ⊓ B.ker := by
  have hA' := zero_le_iff.mp hA
  have hB' := zero_le_iff.mp hB
  ext v; simp only [Submodule.mem_inf, mem_ker_iff_mulVec_zero]
  constructor
  · intro hv
    have h3 : star v ⬝ᵥ A.mat *ᵥ v + star v ⬝ᵥ B.mat *ᵥ v = 0 := by
      rw [← dotProduct_add, ← Matrix.add_mulVec, ← mat_add, hv, dotProduct_zero]
    obtain ⟨hzA, hzB⟩ := (add_eq_zero_iff_of_nonneg (hA'.2 v) (hB'.2 v)).mp h3
    exact ⟨(hA'.dotProduct_mulVec_zero_iff v).mp hzA,
           (hB'.dotProduct_mulVec_zero_iff v).mp hzB⟩
  · simp +contextual [Matrix.add_mulVec]

theorem ker_sum [DecidableEq n] (f : ι → HermitianMat n 𝕜) (hf : ∀ i, 0 ≤ f i) :
    (∑ i, f i).ker = ⨅ i, (f i).ker := by
  ext v
  simp only [Submodule.mem_iInf, mem_ker_iff_mulVec_zero]
  constructor
  · intro hv i
    have hfi := zero_le_iff.mp (hf i)
    rw [← hfi.dotProduct_mulVec_zero_iff]
    have hge : ∀ j, 0 ≤ star v ⬝ᵥ (f j).mat *ᵥ v :=
      fun j ↦ (zero_le_iff.mp (hf j)).2 v
    have hsum : ∑ j, star v ⬝ᵥ (f j).mat *ᵥ v = 0 := by
      rw [← dotProduct_sum, ← Matrix.sum_mulVec, ← mat_finset_sum, hv, dotProduct_zero]
    exact le_antisymm
      (hsum ▸ Finset.single_le_sum (fun j _ => hge j) (Finset.mem_univ i))
      (hge i)
  · intro h
    simp [Matrix.sum_mulVec, h]

theorem ker_conj [DecidableEq n] (hA : 0 ≤ A) (B : Matrix n n 𝕜) :
    (A.conj B).ker = Submodule.comap (Matrix.toEuclideanLin B.conjTranspose) A.ker := by
  --TODO Cleanup
  ext v; simp [HermitianMat.conj];
  constructor <;> intro h;
  · -- By definition of $A$, we know that $⟨w, A w⟩ = 0$ implies $w \in \ker A$.
    have h_inner_zero : ∀ w : EuclideanSpace 𝕜 n, 0 ≤ A → (star w ⬝ᵥ A.mat *ᵥ w) = 0 → w ∈ A.ker := by
      intro w hw h_zero
      apply HermitianMat.mem_ker_of_inner_mulVec_zero hw w h_zero;
    convert h_inner_zero ( Bᴴ *ᵥ v ) hA _;
    convert congr_arg (star v ⬝ᵥ ·) h using 1
    · simp only [Matrix.mulVec_mulVec, dotProduct_comm]
      simp only [dotProduct, Matrix.mulVec, mul_comm, Pi.star_apply, Matrix.conjTranspose_apply,
        RCLike.star_def, star_sum, star_mul', RingHomCompTriple.comp_apply, RingHom.id_apply,
        Finset.mul_sum, mul_left_comm, mul_assoc, lin, mat_mk, ContinuousLinearMap.coe_mk']
      simp only [Matrix.mul_apply, mat_apply, Matrix.conjTranspose_apply, RCLike.star_def, mul_comm,
        Finset.mul_sum, mul_left_comm, Matrix.toEuclideanLin, LinearEquiv.trans_apply,
        Matrix.toLin'_mul, LinearEquiv.arrowCongr_apply, LinearEquiv.symm_symm,
        WithLp.linearEquiv_apply, LinearMap.coe_comp, Function.comp_apply, Matrix.toLin'_apply,
        Matrix.mulVec_mulVec, WithLp.linearEquiv_symm_apply, PiLp.toLp_apply, Matrix.mulVec,
        dotProduct, PiLp.ofLp_apply]
      rw [Finset.sum_comm]
      congr! 1
      rw [Finset.sum_comm]
    · simp
  · simp only [ker, Matrix.mul_assoc, LinearMap.mem_ker]
    convert congr_arg B.toEuclideanLin h using 1
    · simp [HermitianMat.lin, Matrix.toEuclideanLin]
    · exact Eq.symm (LinearMap.map_zero (Matrix.toEuclideanLin B))

theorem ker_le_of_le_smul {α : ℝ} [DecidableEq n] (hα : α ≠ 0) (hA : 0 ≤ A) (hAB : A ≤ α • B) : B.ker ≤ A.ker := by
  rw [← ker_pos_smul B hα]
  exact ker_antitone hA hAB

--TODO: Positivity extensions for traceLeft, traceRight, rpow, nat powers, inverse function,
-- the various `proj` function (in Proj.lean), and the inner product.

/-! ## Positivity extensions connecting HermitianMat and Matrix -/
section MatrixPositivity
open Lean Meta Mathlib.Meta.Positivity

/-- If a HermitianMat is PSD, then its eigenvalues are nonneg. -/
theorem eigenvalues_nonneg [DecidableEq n] (hA : 0 ≤ A) (i : n) :
    0 ≤ A.H.eigenvalues i :=
  (zero_le_iff.mp hA).eigenvalues_nonneg i

open MatrixOrder in
/-- If a HermitianMat is PSD, its underlying matrix is nonneg in the Loewner order. -/
theorem mat_nonneg (hA : 0 ≤ A) : 0 ≤ A.mat :=
  Matrix.nonneg_iff_posSemidef.mpr (zero_le_iff.mp hA)

open MatrixOrder in
/-- If a HermitianMat is positive, its underlying matrix is positive in the Loewner order. -/
theorem mat_pos (hA : 0 < A) : 0 < A.mat :=
  hA

open MatrixOrder in
/-- `Mᴴ * M` is nonneg in the Loewner order, for any matrix `M`. -/
theorem _root_.Matrix.nonneg_conjTranspose_mul_self {m : Type*} [Fintype m]
    (M : Matrix m n 𝕜) : 0 ≤ M.conjTranspose * M :=
  Matrix.nonneg_iff_posSemidef.mpr (Matrix.posSemidef_conjTranspose_mul_self M)

open MatrixOrder in
/-- `M * Mᴴ` is nonneg in the Loewner order, for any matrix `M`. -/
theorem _root_.Matrix.nonneg_self_mul_conjTranspose {m : Type*} [Fintype m]
    (M : Matrix n m 𝕜) : 0 ≤ M * M.conjTranspose :=
  Matrix.nonneg_iff_posSemidef.mpr (Matrix.posSemidef_self_mul_conjTranspose M)

open MatrixOrder in
theorem subtype_mk_nonneg {M : Matrix m m 𝕜} (h : 0 ≤ M) :
    0 ≤ (⟨M, (Matrix.LE.le.posSemidef h).isHermitian⟩ : HermitianMat m 𝕜) :=
  h

open MatrixOrder in
theorem subtype_mk_pos {M : Matrix m m 𝕜} (h : 0 < M) :
    0 < (⟨M, (Matrix.LE.le.posSemidef h.le).isHermitian⟩ : HermitianMat m 𝕜) :=
  h

open MatrixOrder in
private theorem _root_.Matrix.eigenvalues_nonneg [DecidableEq n] {M : Matrix n n 𝕜} (h : 0 ≤ M) (i : n) :
    0 ≤ (Matrix.LE.le.posSemidef h).isHermitian.eigenvalues i :=
  (Matrix.LE.le.posSemidef h).eigenvalues_nonneg i

/-- Positivity extension for `A.mat` where `A : HermitianMat`: nonneg when `0 ≤ A`. -/
@[positivity HermitianMat.mat _]
def evalHermitianMatMat : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app _matFn (A : Expr) ← whnfR e | throwError "not HermitianMat.mat"
  match ← bestResult A with
  | (true, pa) =>
    pure (.positive (← mkAppM ``HermitianMat.mat_pos #[pa]))
  | (false, pa) =>
    pure (.nonnegative (← mkAppM ``HermitianMat.mat_nonneg #[pa]))

/-- Positivity extension for `A.mat` where `A : HermitianMat`: nonneg when `0 ≤ A`. -/
@[positivity Subtype.val (_ : HermitianMat _ _)]
def evalHermitianMatVal : PositivityExt where eval {_u _α} _zα _pα e := do
  /- Note: we must not call `whnf` on `e` because `Subtype.val` is a structure
  projection (reducible), so `whnf` would reduce it and destroy the pattern. -/
  let A := e.appArg!
  match ← bestResult A with
  | (true, pa) =>
    pure (.positive (← mkAppM ``HermitianMat.mat_pos #[pa]))
  | (false, pa) =>
    pure (.nonnegative (← mkAppM ``HermitianMat.mat_nonneg #[pa]))

/-- Positivity extension for `M * Mᴴ` as a Matrix: always nonneg. -/
@[positivity HMul.hMul _ (Matrix.conjTranspose _)]
def evalMatrixSelfMulConjTranspose : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app (.app _hmul _M) Mstar ← whnfR e | throwError "not HMul application"
  let .app _conjTranspose M' ← whnfR Mstar | throwError "not M * conjTranspose"
  pure (.nonnegative (← mkAppM ``Matrix.nonneg_self_mul_conjTranspose #[M']))

/-- Positivity extension for `Mᴴ * M` as a Matrix: always nonneg. -/
@[positivity HMul.hMul (Matrix.conjTranspose _) _]
def evalMatrixConjTransposeMulSelf : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app (.app _hmul Mstar) _M ← whnfR e | throwError "not HMul application"
  let .app _conjTranspose M' ← whnfR Mstar | throwError "not conjTranspose * M"
  pure (.nonnegative (← mkAppM ``Matrix.nonneg_conjTranspose_mul_self #[M']))

/-- Positivity extension for `⟨M, (pf : M.IsHermitian)⟩` as a HermitianMat:
equivalent to `0 ≤ M` in `MatrixOrder`. -/
@[positivity (Subtype.mk _ _ : HermitianMat _ _)]
def evalHermitianMatMk : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app (.app _mkFn val) _proof ← whnfR e | throwError "not Subtype.mk"
  match ← bestResult val with
  | (true, pa) =>
    pure (.positive (← mkAppM ``HermitianMat.subtype_mk_pos #[pa]))
  | (false, pa) =>
    pure (.nonnegative (← mkAppM ``HermitianMat.subtype_mk_nonneg #[pa]))

/-- Positivity extension for eigenvalues of a Matrix: `0 ≤ (_ : M.IsHermitian).eigenvalues i`.
Will try to prove `0 ≤ M` in the `MatrixOrder`. If the proof is `A.H`, i.e. M comes from a
HermitianMat, this will give `0 ≤ A.mat` which becomes `0 ≤ A` later. -/
@[positivity Matrix.IsHermitian.eigenvalues _ _]
def evalMatrixEigenvalues : PositivityExt where eval {_u _α} _zα _pα e := do
  let .app (.app _eigenvaluesFn hProof) _i ← whnfR e | throwError "not eigenvalues application"
  let pType ← inferType hProof
  if pType.isAppOf  ``Matrix.IsHermitian then
    let M ← pure pType.appArg!
    let (_, pa) ← bestResult M
    let pa ← try mkAppM ``le_of_lt #[pa] catch _ => pure pa
    pure (.nonnegative (← mkAppM ``Matrix.eigenvalues_nonneg #[pa, _i]))
  else
    throwError "not a Matrix.IsHermitian"

-- Tests
section tests

variable [DecidableEq n] [DecidableEq m]
open MatrixOrder

-- Test: eigenvalues nonneg from PSD HermitianMat
example (A : HermitianMat n ℂ) (hA : 0 < A) (i : n) : 0 ≤ A.H.eigenvalues i := by
  positivity

-- Test: A.mat nonneg from A nonneg
example (A : HermitianMat n ℂ) (hA : 0 ≤ A) : 0 ≤ A.mat := by positivity
example (A : HermitianMat n ℂ) (hA : 0 < A) : 0 < A.mat := by positivity
example (A : HermitianMat n ℂ) (hA : 0 ≤ A) : 0 ≤ A.val := by positivity
example (A : HermitianMat n ℂ) (hA : 0 < A) : 0 < A.val := by positivity

-- Test: Mᴴ * M nonneg as Matrix
example (M : Matrix m n ℂ) : 0 ≤ M.conjTranspose * M := by positivity

-- Test: M * Mᴴ nonneg as Matrix
example (M : Matrix n m ℂ) : 0 ≤ M * M.conjTranspose := by positivity

-- Test: ⟨Mᴴ * M, _⟩ nonneg as HermitianMat
example (M : Matrix m n ℂ) :
    (0 : HermitianMat n ℂ) ≤ ⟨M.conjTranspose * M, Matrix.isHermitian_transpose_mul_self M⟩ := by
  positivity

-- Test: ⟨M * Mᴴ, _⟩ nonneg as HermitianMat
example (M : Matrix n m ℝ) :
    (0 : HermitianMat n ℝ) ≤ ⟨M * M.conjTranspose, Matrix.isHermitian_mul_conjTranspose_self M⟩ := by
  positivity

example (M : Matrix n n ℂ) (i : n) (A : HermitianMat n ℂ) (hA : 0 ≤ A) :
    0 ≤ (A + ⟨_, M.isHermitian_mul_conjTranspose_self⟩ + 0).H.eigenvalues i := by
  positivity

end tests
end MatrixPositivity
